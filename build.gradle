import com.bmuschko.gradle.docker.DockerRegistryCredentials
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

plugins {
    id 'com.bmuschko.docker-remote-api' version '5.2.0' apply false
    id 'com.gorylenko.gradle-git-properties' version '1.5.1' apply false
    id "org.sonarqube" version "2.7.1" apply false
}

group = 'io.github.gurv'
version = '0.2.0-SNAPSHOT'

apply plugin: 'org.sonarqube'

ext {
    dockerVersion = '18.03.1-ce'
    portainerVersion = '1.22.0'

    email = 'vladimir.gurinovich@gmail.com'
    
    dockerhubRegistry = 'docker.io/gurv'
    githubRegistry = 'ghcr.io/gurv'
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gurv-github"
        property "sonar.projectVersion", "${project.version}"
        property "sonar.sources", "."
        property "sonar.links.ci", "https://travis-ci.org/gurv/vg-ops"
        property "sonar.links.scm", "https://github.com/gurv/vg-ops"
        property "sonar.links.issue", "https://github.com/gurv/vg-ops/issues"
    }
}

subprojects {
    apply plugin: 'base'
    apply plugin: 'com.bmuschko.docker-remote-api'

    ext {
        imageName = "vg-${project.name}"
        imageVersion = "latest"
    }

    docker {
        registryCredentials {
            email = rootProject.email
        }
    }

    task buildImage(type: DockerBuildImage) {
        dockerFile = file('docker/Dockerfile')
        inputDir = project.projectDir
        tags.add("${project.imageName}:${project.imageVersion}")
    }

    task tagImageDockerHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${rootProject.dockerhubRegistry}/${project.imageName}"
        tag = project.imageVersion
    }
    
    task pushImageToDockerHub(type: DockerPushImage) {
        dependsOn tagImageDockerHub
        imageName = "${rootProject.dockerhubRegistry}/${project.imageName}"
        tag = project.imageVersion
        def credentials = new DockerRegistryCredentials(project.objects)
        credentials.url.set rootProject.dockerhubRegistry
        credentials.username.set findProperty('dockerhubUsername') ?: System.getenv('DOCKERHUB_USERNAME')
        credentials.password.set findProperty('dockerhubPassword') ?: System.getenv('DOCKERHUB_TOKEN')
        registryCredentials = credentials
    }

    task tagImageGitHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${rootProject.githubRegistry}/${project.imageName}"
        tag = project.imageVersion
    }

    task pushImageToGitHub(type: DockerPushImage) {
        dependsOn tagImageGitHub
        imageName = "${rootProject.githubRegistry}/${project.imageName}"
        tag = project.imageVersion
        def credentials = new DockerRegistryCredentials(project.objects)
        credentials.url.set rootProject.githubRegistry
        credentials.username.set findProperty('githubUsername') ?: System.getenv('GITHUB_USERNAME')
        credentials.password.set findProperty('githubToken') ?: System.getenv('CONTAINER_REGISTRY_PAT')
        registryCredentials = credentials
    }

    task publish() {
        dependsOn pushImageToGitHub, pushImageToDockerHub
    }
}
